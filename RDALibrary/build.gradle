apply plugin: 'com.android.library'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "2.0.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

//LOCAL DEBUG AAR DOSYA YARATILMASI ++
def devDir = null

Properties properties = null

if (project.rootProject.file('local.properties').exists()) {

    properties = new Properties()

    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

devDir = properties != null ? properties.getProperty("aar.dir") : null
//LOCAL DEBUG AAR DOSYA YARATILMASI --

dependencies {

    //ANDROID SUPPORT
    api 'com.android.support:appcompat-v7:28.0.0'
    api "com.android.support:recyclerview-v7:28.0.0"
    api 'com.android.support:cardview-v7:28.0.0'

    //SWIPE LIST
    api 'com.daimajia.swipelayout:library:1.2.0@aar'

    //GSON
    api 'com.google.code.gson:gson:2.8.5'

    //RDALogger
    api 'com.github.ardakaplan:RDALogger:1.0.0'

    //DAGGER
    implementation "com.google.dagger:dagger:2.16"
    implementation "com.google.dagger:dagger-android-support:2.16"
    annotationProcessor "com.google.dagger:dagger-android-processor:2.16"
    annotationProcessor "com.google.dagger:dagger-compiler:2.16"
}

//LOCAL DEBUG AAR YARATILMASI ++

task sourceJar(type: Jar) {
    from 'src/main/java', 'build/generated/source/apt/release'
    classifier "sources"
}

configurations {
    archives
}

artifacts {
    archives sourceJar
}

if (devDir != null) {

    afterEvaluate {
        assembleDebug.doLast { sourceJar.execute() }.finalizedBy(copyAAR)
    }

    task copyAAR(type: Copy) {

        if (devDir != null) {

            from 'build/outputs/aar', 'build/libs'
            into devDir
            include '**/*debug.aar', '**/*sources.jar'

        }

        outputs.upToDateWhen { false }
    }
}
//LOCAL DEBUG AAR YARATILMASI --